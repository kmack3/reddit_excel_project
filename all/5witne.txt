slow
5witne
Can someone look over my VBA macro code to see why it might be running so slow?

I have a button on a sheet in my workbook that basically does a little bit of copy-pasting on another sheet. There are formulas based off what it pastes into that sheet. In order to speed it up, I disabled calculation and screen updating while it copy-pastes. When it is done, it enables those again.

On my end, this works well and runs in under a second. I've sent it to someone to test on a Mac and they said "When you click the button, it goes directly to Page B and takes 20 seconds to finish running." The macro shouldn't take them to Page B (it doesn't for me) and it shouldn't take that long. Can someone look over this code and see why that may happen on a Mac (but not for me)?

I can go into more detail about the intentions of some of these lines if needed!

    Sub Clicked()
        Application.ScreenUpdating = False
        Application.EnableEvents = False
        Application.Calculation = xlCalculationManual
        Worksheets("Page B").Unprotect "secretpassword"
        Worksheets("Page B").Range("I3:I603") = Worksheets("Page B").Range("M3:M603").Value
        Worksheets("Page B").Range("C3:C603") = Worksheets("Pabe B").Range("L3:L603").Value
        Sheets("Page B").Select
        Range("D3").Select
        ActiveCell.FormulaR1C1 = "=RC[4]"
        Range("D3").Select
        Selection.AutoFill Destination:=Range("D3:D603"), Type:=xlFillDefault
        Range("D3:D603").Select
        Range("A2").Select
        Sheets("Main Page").Select
        ActiveSheet.Unprotect "secretpassword"
        Range("B5").Select
        Selection.ClearContents
        Range("B4").Select
        Selection.ClearContents
        ActiveCell.FormulaR1C1 = "=VLOOKUP(R[2]C,'Page B'!R3C1:R603C4,4,FALSE)"
        Application.Calculation = xlCalculationAutomatic
        Application.EnableEvents = True
        Application.ScreenUpdating = True
        ActiveSheet.Protect "secretpassword", DrawingObjects:=True, Contents:=True, Scenarios:=True
        Worksheets("Draft Log").Protect "secretpassword", DrawingObjects:=True, Contents:=True, Scenarios:=True
    End Sub


-------------
You don't need all those selects. That's a big part of it.

You can turn this:

    Sheets("Page B").Select
    Range("D3").Select
    ActiveCell.FormulaR1C1 = "=RC[4]"

Into this:

    Sheets("Page B").[D3].FormulaR1C1 = "=RC[4]"


-------------
    sub clicked ()
    application.screenupdating = false
    application.enableevents = false
    application.calculation = xlcalculationmanual
    with worksheets("Page B")
        .unprotect "secretpassword"
        .range("I3:I603") = .range("M3:M603").value
        .range(C3:C603") = .range("L3:L603").value
        .range("D3").formulaR1C1 = "=RC[4]"
        .range("D3").autofill destination:= .range("D3:D603"),  type:=xlfilldefault
    end with
    with worksheets("Main Page")
        .unprotect "secrepassword"
        .range("B4:B5").clearcontents
        .range("B4").formulaR1C1 = "=VLOOKUP(R[2]C, 'Page B'!R3C1:R603C4,4,FALSE)"
        .protect "secretpassword", drawingobjects:=True, contents:=true, scenarios:=true
    end with
    worksheets("Draft Log").protect "secretpassword", drawingobjects:=True, contents:=true, scenarios:=true
    application.screenupdating = True 
    application.enableevents = True
    application.calculation = xlcalculationautomatic
    end sub


-------------
Thanks! I removed the "select" statements and sent it over to some Mac users to test. Hopefully that was the main thing!
-------------
You will need to redo anything involving ActiveSheet and ActiveCell as well.

Without a Select statement you will not move around the sheets.
-------------
Yup! Here's what I had drawn up after getting that advice:

    Sub Clicked()
        Application.ScreenUpdating = False
        Application.EnableEvents = False
        Application.Calculation = xlCalculationManual
        Worksheets("Page B").Unprotect "secretpassword"
        Worksheets("Page B").Range("I3:I603") = Worksheets("Page B").Range("M3:M603").Value
        Worksheets("Page B").Range("C3:C603") = Worksheets("Page B").Range("L3:L603").Value
        Worksheets("Page B").Range("D3:D603").FormulaR1C1 = "=RC[4]"
        Worksheets("Page B").Protect "secretpassword", DrawingObjects:=True, Contents:=True, Scenarios:=True
        Worksheets("Main Page").Unprotect "secretpassword"
        Worksheets("Main Page").Range("B5:D5").ClearContents
        Worksheets("Main Page").Range("B4:D4").ClearContents
        Worksheets("Main Page").Range("B4").FormulaR1C1 = "=VLOOKUP(R[2]C,'Page B'!R3C1:R999C4,4,FALSE)"
        Worksheets("Main Page").Protect "secretpassword", DrawingObjects:=True, Contents:=True, Scenarios:=True
        Application.EnableEvents = True
        Application.Calculation = xlCalculationAutomatic
        Sheets("Main Page").Select
        Application.ScreenUpdating = True
    End Sub
-------------
There's probably a few ways you can tidy it up further if you want, but that looks okay, although you still have a Select at the end.

Hopefully though this gets you sorted :)
-------------
Yeah, for some reason, if I didn't add the Select there, it ended up moving focus to the Page B tab when the code finished. I'm assuming that the focus moved because the VLOOKUP references Page B. When I added that select, it ensured that focus was on the Main Page after it finished, which was important. Maybe there are better ways to go about that though.

Also, I'd be happy to hear any ideas about tidying it up further! A lot of people will be using this so I want it to be as clean and tidy as possible :)

Edit: I also saw tjen's post about using With statements, which may help a bit too.
-------------
I would suggest using With as well, if not just for readability.

