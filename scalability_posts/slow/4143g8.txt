slow
4143g8
Slow VBA Script

My script is designed to refresh a page with formulas, take summary data from it and paste at the bottom of a list. It's running very slowly - i feel like it could be much faster. Does anyone see any glaring flaws? The list (FullRange) is about 300 rows long.
    
    Sub CopyData()
    
    Application.ScreenUpdating = False
    
    Dim Cell As Range, MyRange As Range, TopRange As Range
         
        Set TopRange = Sheets("Key").Range("A4")
        Set FullRange = Range(TopRange, TopRange.End(xlDown))
    
        For Each Cell In FullRange
            Sheets("BaseSheet").Range("B1").Value = Cell.Value
            Sheets("BaseSheet").Activate
            ActiveSheet.Range("I1:Q1").Select
            Selection.Copy
            Sheets("Summary").Select
            Range("A65536").End(xlUp).Select
            Selection.Offset(1, 0).Select
            Selection.PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, SkipBlanks _
                :=False, Transpose:=False
        Next Cell
    
    Worksheets("Summary").Move before:=Worksheets(1)
    
    Application.ScreenUpdating = False
        
    End Sub


-------------
Don't really see anything that should makes your macro particularly slow. Are you sure it's not the workbook that's making the macro run slow?
Perhaps someone more experienced with VBA can tell us why the macro is slow but here are some suggestions

Simplify your code by getting rid of all of the selects and merging them together. For example change:

        Sheets("BaseSheet").Activate
        ActiveSheet.Range("I1:Q1").Select
        Selection.Copy

to:

        Sheets("BaseSheet").Range("I1:Q1").Copy

And change

            Sheets("Summary").Select
            Range("A65536").End(xlUp).Select
            Selection.Offset(1, 0).Select
            Selection.PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, SkipBlanks _
           :=False, Transpose:=False

To

            Sheets("Summary").Range("A65536").End(xlUp).Offset(1, 0).PasteSpecial

And lastly change this back to 'True' at the end of your code

           Application.ScreenUpdating = False
-------------
If you have any events that run in response to changing cell values, you'll want to disable events in your code otherwise it gets run over and over again as you copy+paste values. If not, then this is not a big deal:

`Application.EnableEvents = False`  
`Application.EnableEvents = True`

If any of the values you're pasting are being referenced in formulas, you can get a speed increase by disabling automatic calculations until you are done populating all the data, and then re-enabling it at the end:

`Application.Calculation = xlCalculationManual`  
`Application.Calculation = xlCalculationAutomatic`

/u/jfreelov and /u/craygeeboy have the right idea about not selecting sheets/cells when you do not have to.

An alternative to copying+pasting values only is to just assign the value of the old cells to the new cells. To do this the range has to be the same length though, so copying+pasting is more forgiving:

`TargetRange.Value = SourceRange.Value`

One way to safely accomplish for any sized range is to to assign the "Copy" Range to a variable, and then you can dynamically resize the "Paste" Range to match:
    
    Dim vCopy As Variant
    vCopy = Sheets("BaseSheet").Range("I1:Q1").Value
    Sheets("Summary").Range("A65536").End(xlUp).Offset(1, 0).Resize(UBound(vCopy,1),UBound(vCopy,2)).Value = vCopy

I've personally found this to be faster than copying+pasting, but YMMV.
-------------
The selects are a problem, and the fact that you are finding the last row of data in every pass of the for loop.

Make this more sustainable for sheets that extend beyond the 65K limit.

Avoid Copy/Paste. Just write the value directly cell to cell, range to range.

I assume that since you are writing over BaseSheet - B1 on every pass that there are formulas in BaseSheet - I1:Q1 that you are copying to the end of the Summary sheet. It might also speed it up by doing those calculations in VBA rather than going to the spreadsheet every time.

Either way, here is my revision:

	Sub CopyData()

	Application.ScreenUpdating = False

	Dim Cell As Range, MyRange As Range, TopRange As Range
        Dim LRowSummary As Long
	Dim shtSummary As Worksheet
	Dim shtBase As Worksheet
	Dim shtKey As Worksheet
	
	Set shtSummary = ThisWorkbook.Sheets("Summary")
	Set shtBase = ThisWorkbook.Sheets("BaseSheet")
	Set shtKey = ThisWorkbook.Sheets("Key")
	Set TopRange = shtKey.Range("A4")
	Set FullRange = Range(TopRange, TopRange.End(xlDown))
        LRowSummary = shtSummary.Cells(shtSummary.Rows.Count, 1)End(xlUp).Row + 1
		
		For Each Cell In FullRange
			shtBase.Range("B1").Value = Cell.Value
			shtSummary.Range("A" & LRowSummary & ":" & "I" & LRowSummary).Value = shtBase.Range("I1:Q1").Value
			LRowSummary = LRowSummary + 1
		Next Cell

	shtSummary.Move before:=Worksheets(1)

	Set shtBase = Nothing
	Set shtKey = Nothing
	Set shtSummary = Nothing
	Set TopRange = Nothing
	Set FullRange = Nothing
	Application.ScreenUpdating = False

	End Sub
-------------
Another possibility is that Excel might be recalculating the workbook on every loop iteration:  do you have formulas referencing the range BaseSheet!A:A?  If so, use 
     
    Application.ScreenUpdating = False
    Application.Calculation = xlCalculationManual
    
    'The rest of the routine here
    
    Application.ScreenUpdating = True
    Application.Calculation = xlCalculationAutomatic
-------------
It's mostly the selects. craygeeboy beat me to it, but since I already have the VBA copied, here is another similar answer:


    Sub CopyData()
    
    Application.ScreenUpdating = False
    
    Dim Cell As Range, MyRange As Range, TopRange As Range
    
        Set TopRange = Sheets("Key").Range("A4")
        Set FullRange = Range(TopRange, TopRange.End(xlDown))
    
        For Each Cell In FullRange
            Sheets("BaseSheet").Range("B1").Value = Cell.Value
            Sheets("BaseSheet").Range("I1:Q1").Copy
            Sheets("Summary").Range("A65536").End(xlUp).Offset(1, 0).PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, SkipBlanks:=False, Transpose:=False
        Next Cell
    
    Worksheets("Summary").Move before:=Worksheets(1)
    
    Application.ScreenUpdating = False
    
    End Sub

-------------
Thanks! I'll give this all a shot.
-------------
This may have nailed it. Going to give it a try this weekend
