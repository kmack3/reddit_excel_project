big
365p2f
This is a big one; finding a total within a certain month.

So I've got an amount, frequency, start date and end date in columns A, B, C & D respectively. What I've been looking to do (and struggling with immensely) is to find the total amount within a certain month, given that the amount is added each time the frequency comes up. So,an amount of 2 over 30 days would have an overall total of 60 (2 occurances on each day), but I want to be able to break the 60 down into each calendar month. Here is a google doc of the format (feel free to edit it): https://docs.google.com/spreadsheets/d/1tlPp66JOwKttZqm3QRJXoXNOa0uN_3CmBsVEfClWh6g/edit?usp=sharing (american date format)

Even if I don't solve this completely, any help/progress I can get would be appreciated. This one's been on my mind for a while.

**Amount** | **Frequency** | **Start** | **End** | 
:--- | :--- | :--- | :--- | 
1 | Daily | 01/01/2015 | 31/01/2015 | 
2 | Weekly | 15/01/2015 | 15/02/2015 | 
3 | Monthly | 01/02/2015 | 28/02/2015 | 
4 | Daily | 16/02/2015 | 16/03/2015 | 
5 | Weekly | 28/02/2015 | 28/03/2015 | 

For example, with this table I'm looking to find the total amount for February 2015.



-------------
What you want to do isn't possible with the way that you've got your data set up right now. You're showing that we have an amount of 2 between January 15th of 2015 and February 15th 2015, but we don't know how much of that amount came from January, and how much came from February. Since it's only 2, then we could probably assume that it was 1 in each month, but what if the amount were 300? There's no way to know what the split was for each month unless we had each transaction record individually.

The only way that this works is with the Daily or Monthly frequency. Then your formula in G2 would be 

=sumifs(A:A, C:C,">="&datevalue(left(F2, len(F2)-6)&" 1, " & LEFT(RIGHT(F2,5),4)),D:D,"<="&eomonth(datevalue(left(F2, len(F2)-6)&" 1, " & LEFT(RIGHT(F2,5),4)),0))

The formula is much longer than it has to be because of how you've got the monthly total label set up. Here's a suggestion: in F2, change the value to 1/1/2015, then setup the number format to a custom format of mmmm yyyy: which will give you the same look but the underlying numeric value is easier to work with. Then in cell F3 enter the formula =edate(F2,1) and use the same number format all the way down. Then your formula in cell G2 would be: 

=sumifs(A:A,C:C,">="&F2,D:D,"<="&eomonth(F2,0))
-------------
How would you calculate this by hand?
-------------
Sorry, perhaps I should have made it clearer. It's not that the amount is the total The amount occurs on each frequency (if that makes sense). So an amount of 2 over 30 days and a daily frequency would have a total of 60 (2 on each day). Thanks for your response.
-------------
[deleted]
-------------
Showing A1:F6

**Amount** | **Frequency** | **Start** | **End** | **Mnth** | **Total** | 
:--- | :--- | :--- | :--- | :--- | :--- | 
1 | Daily | 01/01/2015 | 31/01/2015 | 01/02/2015 | 60 | 
2 | Weekly | 15/01/2015 | 15/02/2015 |  |  | 
3 | Monthly | 01/02/2015 | 28/02/2015 |  |  | 
4 | Daily | 16/02/2015 | 16/03/2015 |  |  | 
5 | Weekly | 28/02/2015 | 28/03/2015 |  |  | 

with

    =SUM(((IF(D2:D6>EOMONTH(E2,0),EOMONTH(E2,0),D2:D6)-IF(C2:C6<E2,E2,C2:C6)+1)*(E2<=D2:D6)*(EOMONTH(E2,0)>=C2:C6)/(D2:D6-C2:C6+1))*IF("Daily"=B2:B6,D2:D6-C2:C6+1,IF("Weekly"=B2:B6,(D2:D6-C2:C6+1)/7,1))*A2:A6)

entered as array, ctrl-shift-enter, into F2.
-------------
That's very much it, yes
-------------
I think this is it, i'll try it now. so for the fourth row (amount 4, daily frequency) the total for February would be 48, with a total of 64 in March? Thank you.
-------------
Solution verified. This seems to do the trick, thank you :) Since I'm here to learn, would you mind breaking down the formula a bit for me (and anyone else who stumbles across this)?
-------------
I've seen some crazy formulas in this sub, but that one is definitely top 5. We should have a wall of fame or something.
-------------
The lowest of the end date or the end of the month in question, minus, the highest of the start date or beginning of the month, plus a day - gives the overlap with the calendar month. Multiplied by one if the *first day of the calendar month (edit)* date is less or equal to the end date, else by zero, and multiplied by one if the end of the month is greater than or equal to the start date, else by zero, - eliminates consideration of periods entirely outside of the month. This array of values is then divided by the number of days in the periods - gives the overlap as a fraction of the period. Multiplied by the number of days in each respective period or by the number of days divided by 7 or by 1, corresponding to daily, weekly or monthly. Multiplied by the amounts. Gives an array of five numbers resulting from each row, which is then summed.
-------------
You have awarded one point to Antimutt
-------------
/r/excel fellows' favourite five freaky formulae?
-------------
Thank you. I actually need to do this for quarterly and yearly frequencies too, but I'll work on that. Thanks
