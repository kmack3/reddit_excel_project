large
49ouy2
Building a macro to automatically separate a very large sheet into new sheets (or workbooks) based on criteria

I have a very large dataset of physician data. I have two criteria that I would like to separate the data by but don't want to do the work myself. 

Can anyone help me build a macro that will separate the data (by specialty, and state) into new sheets (or workbooks if possible).


-------------
If all the data are in one sheet, you can simply use the filter to filter and then copy and paste the filtered results to another sheet. You don't need a macro for that.
-------------
Alternatively, if you want to create a helper column first that combines specialty and state, you can use this code to easily split the data

    'This code will split data from a Master sheet and create a new worksheet for each unique option in a specific column. In this case of this VBA, column A. It would be pretty easy to update this to update the destination sheets instead of creating new.
    ' Requires reference to Microsoft Scripting Runtime
    Sub filt_sh()
    Dim DestSh As Worksheet     ' Destination worksheet
    Dim SourceSh As Worksheet   ' Worksheet containing source data
    Dim FiltArr() As Variant    ' Values to filter
    Dim dict_uniq As Dictionary ' Dictionary to hold unique values
    Dim dict_key As Variant     ' Dictionary key
    Dim uniq_Arr() As Variant   ' Array of unique values from Dictionary
    Dim i As Long               ' Counter
    Dim LR As Long              ' Last Row
    Dim LC As Long              ' Last column
    Dim SrcRng As Range         ' Source Data Range
    
    ' Create the dictionary. Dictionaries can only contain unique values as Keys
    Set dict_uniq = New Dictionary ' CreateObject("Scripting.Dictionary")
    
    'Set the source sheet by name or sheet number
    Set SourceSh = ActiveWorkbook.Sheets("MainSheet")
    
        ' Find the last row of data in the source sheet. Uses column A. If a different column is fully populated, change "A" to the column
    LR = SourceSh.Cells(rows.Count, "A").End(xlUp).Row
    
        ' Find the last column of data in the source sheet
    LC = SourceSh.Cells(1, Columns.Count).End(xlToLeft).Column
    
        ' Set the full range containing data in the source sheet
    Set SrcRng = SourceSh.Range("A1:" & Cells(LR, LC).Address)
    
    ' Grab the values from the range to filter
    FiltArr = Range("A2:A" & LR)
    
    ' Dictionaries will only hold a value once, duplicates are automatically discarded. This creates the list of unique values
    For i = 1 To UBound(FiltArr)
        dict_uniq(FiltArr(i, 1)) = 1
    Next i
       
    ' Cycle through the array to create worksheets for each unique value and copy data
    For Each dict_key In dict_uniq.keys
    
                ' Create the destination worksheet
                Set DestSh = Worksheets.Add
    
                ' Name the worksheet. It will match the Unique value. If the name has been used the code break here.
                DestSh.Name = dict_key
    
                ' Filter and copy the values from the Source Range
                SrcRng.AutoFilter
                SrcRng.AutoFilter Field:=1, Criteria1:=dict_key
                SrcRng.SpecialCells(xlCellTypeVisible).Copy DestSh.Range("A1")
    Next dict_key
    End Sub

-------------
True, overthinking things here on hump day.

Thanks for bringing me back down to earth
-------------
solved

thanks!
-------------
+1 Point
-------------
Great! Glad to help. Please reply Solution Verified, if you don't mind.
-------------
Sigh. Take it away, OP "Solution Verified" me about 30 minutes after you have me the point.
-------------
Solution verified
-------------
Sometimes-a the OP-a, he come-a back.
-------------

You have awarded one point to _fuzzius_navus_.    
[^Find ^out ^more ^here.](https://www.reddit.com/r/excel/wiki/clippy)

-------------
Si. Grazia. Buona notte!
