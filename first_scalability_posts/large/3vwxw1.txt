large
3vwxw1
Processing Large quantity of Data- Trying to fill in the missing data with the average of the meaningful data point before and the meaningful data point following the missing data points.

currently I'm processing a very large dataset, within my dataset i have many datapoints missing. These points were replaced with -99 during initial processing. However, when I try to take the average, these -99s create problems. Currently I'm trying to make excel replace all "-99" with the the (previous meaningful data point+data point after)/2.



-------------
Bit of a roundabout solution, but it's a pretty easy one...

If you can add some helper columns... Column K will be headed "Count" and would just contain the formula =IF(J2>0,1,0), giving you either a 0 or 1 in the column.  0 for all -99s, 1 for everything else.

In Column L you'll have "Sum" and contain the formula =IF(K2=1,SUM($K$2:K2),"").  This will keep a running total the 1s, and blanks next to the 0s.

In Column M...
=IF(L2<>"",J2,AVERAGE(INDEX($J$2:$L$1000,MATCH(MAX($L$2:L2),$L$2:$L$1000,0),1),INDEX($J$2:$L$1000,MATCH(MIN(L2:$L$1000),$L$2:$L$1000,0),1)))

Basically, if L2 (your "Sum" column) isn't blank, then take the number from J.  If it is blank, find the max number number in the sum column up to that row (so the last non "-99" cell) and the min number in the sum column after that row (so the next non "-99" cell) and average them.  This is obviously for row 2, and then you can just drag down across all rows :)

This also assumes your data set is only 1000 rows, so you'll need to change that in the formula if it's more, but it should be relatively well explained (I hope!).
-------------
for example here [Imgur](http://i.imgur.com/Aa9kTyH.jpg), i want to replace all the -99 with average of 5.8 and 2.9. 
-------------
Is the (previous meaningful data point+data point after)/2 referencing a static cell? Or at least a consistent range? 

If it's the former, it's a simple find and replace where you just enter the (previous meaningful data point+data point after)/2 into the replace with cell and -99 into the replace cell.

If it's the latter, you could sort by the amount, enter the (previous meaningful data point+data point after)/2 into the first of the -99s and drag it down?
-------------
I would put a third colum with a formula that calculates the average only if the value is -99 or copy the value. Then you copy/paste values only. For example, with
a1=4.5 
a2=-99 
a3 =5.6 
a4=9.5
ax=...

You'd put in b2 to bx 

    if(a2=-99,;avg(a1,a3),;a2)

Once you've dragged the formula down, you copy/paste values only  the column on A:A.

Although, it'll work only if there's no two -99 following each other.
-------------
Sounds like a few helper columns would help you. 

In one column, let's say column E, do: 

    =IF((A2=-99)*(A1<>-99)*(A3<>-99),(A1+A2)/2,IF((A2=-99)*(A1<>-99),A1/2,IF((A2=-99)*(A3<>-99),A3/2,0) 

In anther column, let's say F, do:

    =IF((A2=-99)*(A1<>-99),ROW(),IF(A2=-99,E1,0) 

Then in column G you do: 

    =IF(A2=-99,SUMIFS(E:E,F:F,F2,),A2) 

Copy column G, paste as values


The helper in column E returns the average value if there is only one -99 in this sequence, if there's more than one it pulls the value above for the first -99, divided by two, and the value below for the last -99 divided by two, for all other -99's in between, it returns 0.   
(Dividing by two at this point means you can avoid doing it later, if you wanted, you could remove the /2 everywhere (including the first IF) and insert it in Column G instead.)

Column F marks each sequence of -99's with a unique value based on the row of the first -99 in this sequence. The value of the identifier is pretty irrelevant, what is important is that they are all different for each sequence, but the same for the -99's within the sequence. 

Column G recreates your column A, but replaces the -99 values in each sequence with the sum of the values in column E (so the start and the end value) for each sequence.   
(since we divided by two already, the sum of the tow numbers will be the average) 


-------------
What about linear interpolation ?
-------------
Here's a no helper column solution:

Select your column, go to Find and Replace. Find = -99 Replace with:

    =average(indirect("R[-1]C",FALSE),indirect("R[1]C",FALSE))

After you do this, I think you'll have to hit F9 a bunch of times to get the values to converge because this is kind of circular.
-------------
Posted down the comments but for anyone else interested:

No.| Formula
---|---
5| 5
-99| =IF(A3<>-99,A3,IF(A2=-99,B2,(AVERAGE(B1:B2))))
6| text
9| text
6| text
7| text
-99| text
-99| text
6| text
8| text
4| text
2| text
3| text
6| text

In this, drag the formula down column B and that should then return the average of the two most recent non -99 values. (Cell No. as A1)
-------------
The (previous meaningful data point+data point after) refers to the data which is not -99 before the -99, for example in a range 5,-99,6,9,6,7,-99,-99,6,8,4,2,3,6
the first -99, i will replace it with the mean of 5 and 6,

the second -99 since the data point following it is -99, it will take the average of 7 and 6, same applies to the third -99

does that make sense?
-------------
So based off your imgur example link, all 4 of those orange cells should be 4.35?
-------------
In your example. would it not make more sense that the third -99 be replaced with the average of what replaces the second -99--> (6+7/2)=6.5 and and what follow the -99 -->6?
-------------
Alrighty, here we go:

No.| Formula
---|---
5| 5
-99| =IF(A3<>-99,A3,IF(A2=-99,B2,(AVERAGE(B1:B2))))
6| text
9| text
6| text
7| text
-99| text
-99| text
6| text
8| text
4| text
2| text
3| text
6| text

In this, drag the formula down column B and that should then return the average of the two most recent non -99 values.
-------------
> Basically, if L2 (your "Sum" column) isn't blank, then take the number from J. If it is blank, find the max number number in the sum column up to that row (so the last non "-99" cell) and the min number in the sum column after that row (so the next non "-99" cell) and average them. This is obviously f

yes

