6i8ucj
Finding column header of a column that has two minimum values in separate rows
I have a table (simplified version) with three rows. Row one is a category name. Rows 2 and 3 are two different value categories. I want to find the category name of the column that has a minimum value of row 2 AND a (separate) minimum value of row 3. I'm thinking it will be some version of an HLOOKUP but I can't figure out a good way to perform the logic of two separate range min() functions. 

Hope that makes sense


-------------
Will there always be a column matching this criteria? If yes, and there's only one possible answer, this'll work:

    =INDEX(A1:D1,SUMPRODUCT(COLUMN(A2:D2)*(A2:D2=MIN(A2:D2))*(A3:D3=MIN(A3:D3))))
-------------
Not sure what you mean "will there always be a column". There could potentially be duplicates (I.e., two columns with the same *value 1*, two columns with the same *value 2*, or two columns with the same *value 1* AND *value 2*. That third case wouldn't matter if you only returned the first occurrence, but the first two cases would still need to find the logical AND despite non-unique individual values.

I'll give your formula a try. 
-------------
**Examples**

Here's a table with no column matching your criteria, as Group B has min in row 2, but Group C has min in row 3

Group A|Group B|Group C
---|---|---
2|1|3
4|2|1

This next one has multiple answers, as both Group B and Group C have min values in row 2 and 3.

Group A|Group B|Group C
---|---|---
2|1|1
4|1|1

However, something like this will work just fine:

Group A|Group B|Group C
---|---|---
2|1|1
4|1|3

-------------
I see what you are saying. I think I misrepresented my logic. It should be "of the groups meeting min(value 1) return min(value 2). 
-------------
Ah. THen perhaps this:

  
    =INDEX(A1:C1,MATCH(TRUE,IF(A2:C2=MIN(A2:C2),A3:C3)=
     MIN(IF(A2:C2=MIN(A2:C2),A3:C3)),0))

With this sample, would return Group C.

Group A|Group B|Group C
---|---|---
2|4|2
4|1|3

-------------
Sorry, but I get #N/A with the test sample you have and that formula. [Heres a screenshot](https://imgur.com/gallery/hKM0wOW)

I'm not good enough with this stuff to see if there is a simple syntax error, but I didn't see one. Why are we matching to a value of TRUE?
-------------
sorry, I forgot to say the above was an array formula, have to confirm via Ctrl+Shift+enter, not just Enter.

The inner IF functions are building an array. The first one says that if the value in row 2 is the min value, return the number from row 3. So, this array will look like: `4, FALSE, 3`. Then, it checks if that is equal to the min value from that array (the 2nd IF function). So, checking each item in that array if it's =3, changes array to `FALSE, FALSE, TRUE`. This is where the MATCH on True happens. We find that the True is in the 3rd position, letting our INDEX pull the 3rd item (Group C).
-------------
Major excel education going on here. Thanks for the help. I really appreciate it!
